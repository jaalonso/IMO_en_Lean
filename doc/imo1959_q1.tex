\section{IMO 1959 Q1}\label{inq159}

En esta sección se va a detallar la solución al problema Q1
correspondiente al año 1959. En primer lugar, se presentarán el
enunciado y su solución en lenguaje natural, de manera que ésta última
se asemeje a la solución que posteriormente se formalizará en
Lean. Luego, se analizará de manera detallada dicha formalización del
problema en Lean.

Destacar que la formalización original del problema se ha
obtenido de \cite{KL} y fue propuesta por Kevin Lacker.

\noindent
\framebox{\parbox{\textwidth}{
  \textbf{Problema 1 (1959--Q1)}. Demostrar que la fracción
  \[\frac{21⋅n+4}{14⋅n+3}\]
  es irreducible para cualquier número natural \(n\).}}

Para ver que esta fracción es irreducible para cualquier número natural
\(n\) se demostrará que el numerador y el denominador de ésta son dos
números coprimos independientemente del valor de \(n\).

Antes de proseguir, introduzcamos el concepto de que dos números sean
coprimos:

\begin{definicion}\label{coprimos}
  Se dirá que dos números naturales \(a\) y \(b\) son \textbf{coprimos}
  o \textbf{primos relativos} si no tienen ningún factor primo en común,
  o equivalentemente, si no poseen otro divisor común distinto de \(1\).
\end{definicion}

La correspondiente formalización en Lean de esta definición es:

\begin{leancode}
  def coprime (a b : ℕ) : Prop := gcd a b = 1
\end{leancode}
\indLema{coprime}

A continuación, se va a introducir un lema auxiliar, el cual nos dirá
que si existe un número natural \(k\) que divide al numerador y al
denominador de la fracción anterior; entonces, dicho número \(k\)
dividide a \(1\).

\begin{lema}[Auxiliar]\label{lema}
  Sean \(k\) y \(n\) dos números naturales tales que \(k\) divide al
  numerador y al denominador de la fracción anterior; es decir,
  \begin{align}
    & k \mid (21⋅n+4)   \label{h1}\tag{h1} \\
    & k \mid (14⋅n+3).  \label{h2}\tag{h2}
  \end{align}
  Entonces se tiene que \(k\) divide a \(1\), para cualquier número
  natural \(n\).
\end{lema}

\begin{demostracion}
  Como consecuencia de que \(k\) divida al numerador de la fracción,
  (\ref{h1}), se tiene que también divide a un múltiplo cualquiera de
  éste; es decir,
  \begin{equation}\label{h3}\tag{h3}
    k \mid 2⋅(21⋅n+4),
  \end{equation}
  donde en particular hemos considerado el múltiplo resultante de
  multiplicar por \(2\).

  Análogamente para el caso del denominador, como se verifica
  (\ref{h2}), se puede escribir que
  \begin{equation}\label{h4}\tag{h4}
    k \mid 3⋅(14⋅n+3),
  \end{equation}
  donde en este caso se ha considerado el múltiplo resultante de
  multiplicar por \(3\).

  Por otro lado, se tiene que desarrollando los productos de (\ref{h3}) y
  (\ref{h4}) como sigue:
  \begin{align}
    & 2⋅(21⋅n+4)=42⋅n+8    \\
    & 3·(14·n+3)=42·n+9,
  \end{align}
  se puede observar que para todo número natural \(n\), se verifica la
  siguiente relación entre el múltiplo del numerador y el del denominador:
  \begin{equation}\label{h5}\tag{h5}
    3⋅(14⋅n+3)=2⋅(21⋅n+4)+1.
  \end{equation}

  Ahora bien, como consecuencia de que \(k\) sea divisor de
  \(3⋅(14⋅n+4)\), (\ref{h4}), y la igualdad vista en (\ref{h5}), se llega
  a que:
  \begin{equation}\label{div1b}
    k \mid 2⋅(21⋅n+4)+1
  \end{equation}

  Para concluir la demostración de este lema, basta con usar la siguiente
  propiedad de la división en los números naturales:

  \begin{proposicion}\label{divn}
    Sean \(k,m\) y \(n\) tres números naturales tales que \(k\) divide a
    \(m\). Entonces se tiene que \(k\) divide a la suma de \(m\) y \(n\)
    si y solamente si \(k\) divide a \(n\). Simbólicamente,
    \begin{equation}
      ∀ \{k, m, n ∈ ℕ\}, k ∣ m → (k ∣ m + n ↔ k ∣ n)
    \end{equation}
  \end{proposicion}

  De esta forma, aplicando la propiedad \ref{divn} sobre (\ref{div1b}),
  se llega a que \(k\) divide a \(1\); que era lo que queríamos
  demostrar.
\end{demostracion}

A continuación vamos a ver la formalización en Lean de este Lema:

\begin{leancode}
import tactic.ring
import data.nat.prime

open nat

lemma Auxiliar
  (n k : ℕ)
  (h1 : k ∣ 21 * n + 4)
  (h2 : k ∣ 14 * n + 3)
  : k ∣ 1 :=
begin
  have h3 : k ∣ 2 * (21 * n + 4),
    from dvd_mul_of_dvd_right h1 2,
  have h4 : k ∣ 3 * (14 * n + 3),
    from dvd_mul_of_dvd_right h2 3,
  have h5 : 3 * (14 * n + 3) = 2 * (21 * n + 4) + 1,
    { ring, },
  rw h5 at h4,
  rw ← nat.dvd_add_right h3,
  exact h4,
end
\end{leancode}
\index{Auxiliar}

En la prueba del lema se han usado los siguientes lemas:
\begin{itemize}
\item \mint{lean}|dvd_mul_of_dvd_right: a ∣ b → ∀ c, a ∣ c * b|
  \indLema{dvd\_mul\_of\_dvd\_right}
\item \mint{lean}|nat.dvd_add_right: ∀ {k m n : ℕ}, k ∣ m → (k ∣ m + n ↔ k ∣ n)|
  \indLema{nat.dvd\_add\_right}
\end{itemize}
y las tácticas
\tactica{from}{from},
\tactica{have}{have},
\tactica{ring}{ring} y
\tactica{rw / rewrite}{rw}.

Finalmente, aplicando el Lema Auxiliar \ref{lema} se puede concluir que para
cualquier número natural \(n\) se verifica que los números \(21⋅n+4\)
y \(14⋅n+3\) son coprimos. O, equivalentemente, que la fracción
\begin{equation*}
  \frac{21⋅n+4}{14⋅n+3}
\end{equation*}
es irreducible. Esta afirmación se tiene como consecuencia de la
definición de dos números coprimos, vista en \ref{coprimos}.

A continuación, vamos a introducir la formalización en Lean propuesta:

\begin{leancode}
theorem imo1959_q1 : ∀ n : ℕ, coprime (21 * n + 4) (14 * n + 3) :=
begin
  assume n,
  apply coprime_of_dvd',
  intros k hk h1 h2,
  exact Auxiliar n k h1 h2,
end
\end{leancode}
\index{imo1959\_q1}

En la demostración del teorema se ha usado el siguiente lema:
\begin{itemize}
\item \mint{lean}|coprime_of_dvd' :
  (∀ k, prime k → k ∣ m → k ∣ n → k ∣ 1) → coprime m n|
  \indLema{coprime\_of\_dvd'}
\end{itemize}
y las tácticas
\tactica{apply}{apply},
\tactica{assume}{assume},
\tactica{exact}{exact} y
\tactica{intro / intros}{intros}.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "IMO_en_Lean"
%%% End:
