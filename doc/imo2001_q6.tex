\section{IMO 2001 Q6}

En esta nueva sección, se va a detallar la resolución del problema Q6
propuesto en el año 2001. Como en el resto de planteamientos anteoriores
que se han detallado en el capítulo previo, se comenzará presentado la
demostración en lenguaje natural del problema y luego su correspondiente
formalización en Lean.

\noindent
\framebox{\parbox{\textwidth}{
    \textbf{Problema 1 (2001--Q6)}. Sean \(a\), \(b\), \(c\) y \(d\) cuatro
    números enteros tales que \(a > b > c > d > 0\). Supongamos que
    \begin{equation*}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation*}
    Demostrar que \(ab+cd\) no es primo.
  }}

Para llevar a cabo la formalización en Lean de la resolución de este
problema, ha sido necesaria la importación de las siguientes teorías:
\begin{itemize}
\item
  \href{https://github.com/leanprover-community/mathlib/blob/master/src/data/int/basic.lean}{data.int.basic},
  para poder trabajar con el conjunto de los números enteros.
\item
  \href{https://github.com/leanprover-community/mathlib/blob/master/src/algebra/associated.lean}{algebra.associated},
  para trabajar con las definición de número primo.
\item
  \href{https://github.com/leanprover-community/mathlib/tree/master/src/tactic}{tactic},
  para poder acceder a las diferentes tácticas.
\end{itemize}

La importación en Lean de todas estas teorías se formaliza como sigue:
\begin{leancode}
import data.int.basic
import algebra.associated
import tactic
\end{leancode}

Además, en este problema hay que definir las variables \(a\), \(b\),
\(c\) y \(d\) que pertenecen al conjunto de los números enteros. Las
diferentes desigualdades que verifican se enunciarán en los lemas en los
que sean necesarias dichas condiciones. En Lean esto se formaliza de la
siguiente manera:
\begin{leancode}
variables {a b c d : ℤ}
\end{leancode}

A continuación, se va a dividir en dos partes esta sección: en la
primera parte probaremos cinco resultados auxiliares que han sido
necesarios para la formalización del problema y en la segunda parte se
formalizará el teorema final que nos da el resultado deseado.

\subsection{Resultados auxiliares}

Como ya se ha dicho anteriormente, en esta sección del problema se van a
formalizar cinco resultados auxiliares. Veamos estos lemas con sus
correspondientes formalizaciones en Lean.

Destacar que en los lemas auxiliares que se van a presentar a
continuación no siempre es necesario usar todas las hipótesis del
teorema en sí. Es por eso, que los resultados se han probado usando el
menor número de hipótesis posible, haciendo el resultado lo más general
posible.

\begin{lema}[sumas\_equivalentes]\label{q601lemasuma}
  Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros tales que
  \begin{equation}\label{q601hipsum}\tag{h}
    ac+bd = (a+b-c+d)(-a+b+c+d).
  \end{equation}
  Entonces se verifica que
  \begin{equation}\label{q601objsum}
    b²+bd+d² = a²-ac+c².
  \end{equation}
\end{lema}

\begin{demostracion}
  Para demostrar (\ref{q601objsum}) se comenzará desarrollando la
  segunda parte de la igualdad de la hipótesis (\ref{q601hipsum}),
  obteniendo el siguiente resultado:
  \begin{equation}\tag{h1}\label{q601h1sum}
    (a+b-c+d)(-a+b+c+d) = -a²+b²+ac-c²+bd+d²+ac+bd.
  \end{equation}

  A continuación, reescribimos el segundo término de la hipótesis
  (\ref{q601hipsum}) como se ha descrito en (\ref{q601h1sum}). De esta
  forma se llega a la siguiente igualdad:
  \begin{equation}\label{q601hip2sum}
    ac+bd = -a²+b²+ac-c²+bd+d²+ac+bd.
  \end{equation}

  La conclusión de la prueba ya se tiene, de manera trivial, sin más que
  simplificar la ecuación (\ref{q601hip2sum}) y redistribuir los
  términos se obtiene (\ref{q601objsum}).
\end{demostracion}

Este resultado se formaliza en Lean como a continuación se plantea:
\begin{leancode}
lemma sumas_equivalentes
  (h : a*c + b*d = (a + b - c + d) * (-a + b + c + d))
  : b^2 + b*d + d^2 = a^2 - a*c + c^2 :=
begin
  have h1: (a + b - c + d) * (-a + b + c + d) =
           -a^2  + b^2  + a*c - c^2 + b*d + d^2 + a*c + b*d,
    by ring,
  rw h1 at h,
  by nlinarith,
end
\end{leancode}
\index{sumas\_equivalentes}

Para levar a cabo la formalización de este lema no hemos hecho uso de
ningún lema auxiliar. No obstante, sí se han utilizado las tácticas de
\tactica{have}{have},
\tactica{rw / rewrite}{rw},
\tactica{ring}{ring} y
\tactica{nlinarith}{nlinarith}.

Prosigamos con la demostración del problema, para ello el lema que se
presenta a continuación nos dará la igualdad de dos productos que será
de gran utilidad en la conclusión del problema.

\begin{lema}[productos\_equivalentes]\label{q601lemaprod}
    Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros verificando que
    \begin{equation}\label{q601hipprod}\tag{h}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation}
    Entonces se verifica que
    \begin{equation}\label{q601objprod}
      (ac+bd)(b²+bd+d²) = (ab+cd)(ad+bc).
    \end{equation}
\end{lema}

\begin{demostracion}
  Para llevar a cabo la prueba de este resultado, se van a desarrollar
  los dos lados de la igualdad que se quiere demostrar,
  (\ref{q601objprod}), y se va a comprobar que son iguales.

  Comencemos desarrollando la parte izquierda de (\ref{q601objprod}):
  \begin{equation}\label{q601h1prod}\tag{h1}
    (ac+bd)(b²+bd+d²) = ac(b²+bd+b²)+bd(b²+bd+b²).
  \end{equation}

  Ahora bien, haciendo uso del lema que hemos demostrado antes, es decir,
  el lema \ref{q601lemasuma} sobre el segundo término que aparece sobre
  el que se puede aplicar, se tiene que
  \begin{equation}\label{q601h2prod}\tag{h2}
    ac(b²+bd+b²)+bd(b²+bd+b²) = ac(b²+bd+b²)+bd(a²-ac+c²).
  \end{equation}

  De esta forma, introduciendo el resultado obtenido en (\ref{q601h2prod})
  en (\ref{q601h1prod}), se llega a que:
  \begin{equation}\label{q601pr1}
     (ac+bd)(b²+bd+d²) = ac(b²+bd+b²)+bd(a²-ac+c²).
  \end{equation}

  Ahora bien, si seguimos desarrollando la segunda parte de la igualdad
  (\ref{q601pr1}), se llega a que:
  \begin{equation}\label{q601pr2}
     (ac+bd)(b²+bd+d²) = acb²+acd²+a²bd+bcd².
  \end{equation}

  Por otro lado, se tiene que reescribiendo la parte derecha de la ecuación
  que queremos demostrar,es decir, de (\ref{q601objprod}), se llega a:
  \begin{equation}\label{q601pr3}
    (ab+cd)(ad+bc) = acb²+acd²+a²bd+bcd².
  \end{equation}

  Se puede comprobar a través de las expresiones (\ref{q601pr2}) y
  (\ref{q601pr3}) que llegamos a lo mismo al desarrollar los dos
  términos de la expresión (\ref{q601objprod}). Por tanto, ya se tendría
  la prueba del lema.
\end{demostracion}

En este caso, la formalización en Lean es:
\begin{leancode}
lemma productos_equivalentes
  (h : a*c + b*d = (a + b - c + d) * (-a + b + c + d))
  : (a*c + b*d) * (b^2 + b*d + d^2) = (a*b + c*d) * (a*d + b*c) :=
begin
  have h1: (a*c + b*d) * (b^2 + b*d + d^2) =
           a*c * (b^2 + b*d + d^2) + b*d * (b^2 + b*d + d^2),
    by ring,
  have h2: a*c * (b^2 + b*d + d^2) + b*d * (b^2 + b*d + d^2) =
           a*c * (b^2 + b*d + d^2) + b*d * (a^2 - a*c + c^2),
    by rw sumas_equivalentes h,
  rw h2 at h1,
  by nlinarith,
end
\end{leancode}
\index{productos\_equivalentes}

A continuación, se van a probar dos lemas muy parecidos (puesto que
ambos nos afirman la veracidad de una desigualdad), que a su vez son muy
sencillos de formalizar en Lean bajo las hipótesis del problema.

\begin{lema}[desigualdad\_auxiliar1]\label{lemades1}
  Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros tales que
  \(a > b > c > d \). Además, supongamos que
  estos números enteros verifican que
    \begin{equation}\tag{h}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation}
    Entonces se tiene que
    \begin{equation}\label{q601objdes1}
      ac+bd < ab+cd
    \end{equation}
\end{lema}

\begin{demostracion}
    En primer lugar, denotemos las hipótesis del enunciado como sigue:
  \begin{align}
    &b<a,\tag{hba}\label{hba01des1}\\
    &c<b,\tag{hcb}\label{hcb01des1}\\
    &d<c.\tag{hdc}\label{hdc01des1}
  \end{align}

  Por otro lado, se tiene que demostrar (\ref{q601objdes1}) es equivalente
  a demostrar la siguiente desigualdad:
  \begin{equation}\label{q601objdes12}
      0<ab+cd-(ac+bd).
  \end{equation}

  Sin embargo, desarrollando la resta de (\ref{q601objdes12}), se obtiene
  que el objetivo a probar se convierte en:
  \begin{equation}\label{q601objdes13}
      0<(a-d)(b-c).
  \end{equation}

  No obstante, este resultado es inmediato a partir de las desigualdades
  que tenemos de hipótesis, es decir, (\ref{hba01des1}), (\ref{hcb01des1})
  y (\ref{hdc01des1}).
\end{demostracion}

Formalizar este lema en Lean es bastante sencillo y directo ya que
existe una táctica que a través de las desigualdades que se tienen por
hipótesis, deduce el resultado. La formalización sería:
\begin{leancode}
lemma desigualdad_auxiliar1
  (hba : b < a)
  (hcb : c < b)
  (hdc : d < c)
  (h : a*c + b*d = (a + b - c + d) * (-a + b + c + d))
  : a*c + b*d < a*b + c*d:=
by nlinarith
\end{leancode}
\index{desigualdad\_auxiliar1}

En la demostración de este lema sólo ha sido necesario el uso de la
táctica \tactica{nlinarith}{nlinarith}.

Destacar que la la táctica \tactica{nlinarith}{nlinarith} automatiza todos
los pasos que se han realizado en la demostración en lenguaje natural.

\begin{lema}[desigualdad\_auxiliar2]\label{lemades2}
  Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros tales que \(a\) es
  mayor que \(b\) y \(c\) mayor que \(d\). Es decir,
  \begin{align}
    &b<a,\tag{hba}\label{hba01des2}\\
    &d<c.\tag{hdc}\label{hdc01des2}
  \end{align}
  Además, los números enteros en consideración verifican la siguiente relación:
    \begin{equation}\tag{h}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation}
    Entonces se tiene que
    \begin{equation}\label{des2q601}
      ad+bc < ac+bd
    \end{equation}
\end{lema}

\begin{demostracion}
  En primer lugar, se tiene que demostrar (\ref{des2q601}) es equivalente a
  probar la siguiente expresión:
  \begin{equation}\label{des2q601obj}
      0<ac+bd-(ad+bc),
  \end{equation}
  donde simplemente se ha pasado el término de la izquierda hacia la parte
  derecha de la desigualdad.

  Desarrollando la resta que se puede observar en (\ref{des2q601obj}), se
  obtiene lo siguiente:
  \begin{equation}
    0<(a-b)(c-d).
  \end{equation}

  No obstante, este reultado se tiene de manera inmediate a partir de las
  hipótesis (\ref{hba01des2}) y (\ref{hdc01des2}).
\end{demostracion}

Análogamente al lema anterior, \ref{lemades1}, la formalización en Lean de
este lema sería inmediata:
\begin{leancode}
lemma desigualdad_auxiliar2
  (hba : b < a)
  (hdc : d < c)
  (h : a*c + b*d = (a + b - c + d)*(-a + b + c + d))
  : a*d + b*c < a*c + b*d:=
by nlinarith
\end{leancode}
\index{desigualdad\_auxiliar2}

Al igual que en el lema \ref{lemades1}, sólo ha sido necesario el uso de
la táctica \tactica{nlinarith}{nlinarith} para su formalización. Al igual
que antes, esta táctica prueba de manera automática el resultado.

Finalmente, introduzcamos el último lema auxiliar antes de llevar a cabo
la formalización del problema como tal.

\begin{lema}[division]\label{lemadivisionq6}
  Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros tales que
  verifican la siguiente relación:
    \begin{equation}\tag{h}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation}
    Entonces se tiene que el número entero \(ac+bd\) divide al producto de
    los números enteros \(ab+cd\) y \(ad+bd\). Simbólicamente:
    \begin{equation}
      ac+bd ∣ (ab+cd)(ad+bc).
    \end{equation}
\end{lema}

\begin{demostracion}
  Previo a la demostración como tal del resultado, recordemos qué significa que
  un número entero divida a otro.
  \begin{definicion}\label{defdiventeros}
    Sean \(a\) y \(b\) dos números enteros. Se dirá que \(a\)
    \textbf{divide a} \(b\) si y solamente si existe otro número entero
    \(k\) tal que \(b = a · k\).  Simbólicamente,
    \begin{equation}
    a \mid b ⟺ ∃ k ∈ ℤ, b = a · k.
    \end{equation}
  \end{definicion}

  Por tanto, se tiene que haciendo uso de la definición \ref{defdiventeros},
  la prueba del lema consiste en probar que existe un número entero \(k\)
  verificando que
  \begin{equation}
    (ab+cd)(ad+bc)=(ac+bd)k.
  \end{equation}

  Usando como número entero \(k\) el que viene dado por la suma \(b²+bd+d²\),
  se tendría que para obtener el resultado que se quería demostrar, bastaría
  con hacer uso del Lema \ref{q601lemaprod}.
\end{demostracion}

La formalización en Lean de este resultado sería:
\begin{leancode}
lemma division
  (h : a*c + b*d = (a + b - c + d) * (-a + b + c + d))
  : a*c + b*d ∣ (a*b+c*d) * (a*d+b*c):=
begin
  use (b^2 + b*d +d^2),
  by nlinarith [productos_equivalentes h],
end
\end{leancode}
\index{division}

Para la formalización de este último lema auxiliar sólo han sido necesario el
uso de dos tácticas: \tactica{nlinarith}{nlinarith} y \tactica{use}{use}.

\subsection{Conclusión del problema}

Conluiremos el problema en esta sección, para ello se hará uso de los
lemas auxiliares probados anteriormente. Para resolver el problema que
nos interesa, se enunciará el siguiente teorema:

\begin{teorema}[imo2001q6]
  Sean \(a\), \(b\), \(c\) y \(d\) cuatro números enteros tales que
  \(a > b > c > d > 0\). Además, supongamos que estos números enteros
  verifican que
    \begin{equation}\tag{h}\label{h01q6teor}
      ac+bd = (a+b-c+d)(-a+b+c+d).
    \end{equation}
    Entonces se tiene que \(ab+cd\) no es un número primo.
\end{teorema}
\begin{demostracion}
  En primer lugar, a parte de la hipótesis (\ref{h01q6teor}) enunciada,
  denotemos el resto de hipótesis del teorema como sigue:
  \begin{align}
    &0<d,\tag{hd}\label{hd01q6teor}\\
    &d<c,\tag{hdc}\label{hdc01q6teor}\\
    &c<b,\tag{hcb}\label{hcb01q6teor}\\
    &b<a.\tag{hba}\label{hba01q6teor}
  \end{align}

  A continuación, la prueba del resultado se va a hacer mediante la
  \textbf{introducción de la negación}. Esto es, como el objetivo es demostrar
  que el número \(ab+cd\) no es un número primo, supondremos que sí lo es y
  buscaremos llegar a una contradicción.

  Comencemos introduciendo la hipótesis de que \(ab+cd\) es un número primo:
  \begin{equation}\tag{h1}\label{h101q6teor}
    \text{primo}(ab+dc)
  \end{equation}

  Encadenando las desigualdades que tenemos como hipótesis, es decir,
  (\ref{hd01q6teor}), (\ref{hdc01q6teor}), (\ref{hcb01q6teor}) y
  (\ref{hba01q6teor}), se puede llegar fácilmente a los siguientes tres
  resultados:
  \begin{align}
    &0<a,\tag{ha}\label{ha01q6teor}\\
    &0<b,\tag{hb}\label{hb01q6teor}\\
    &0<c.\tag{hc}\label{hc01q6teor}
  \end{align}

  Ahora bien, haciendo uso del lema \ref{q601lemaprod} (lo cual es posible
  pues tenemos como hipótesis (\ref{h01q6teor})), se obtiene la siguiente
  expresión:
  \begin{equation}\tag{h2}
    (ac+bd)(b²+bd+d²) = (ab+cd)(ad+bc).
  \end{equation}

  Asimismo, haciendo uso del lema \ref{lemadivisionq6}, se llega a que
  \begin{equation}\tag{h3}\label{h301q6teor}
    (ac+bd) | (ab+cd)(ad+bc).
  \end{equation}

  Para proseguir con la prueba del teorema es necesario introducir un
  resultado auxiliar y que es el siguiente:
  \begin{lema}\label{lemaprimo}
    Sean \(a\), \(b\) y \(p\) tres números cualquiera, verificando que \(p\)
    es un número primo y que \(a\) divide al producto de \(p\) y \(b\).
    Entonces se tiene que o bien \(p\) divide a \(a\) ó \(a\) divide a \(b\).
    Simbólicamente:
    \begin{equation}
      ∀ a,b,p, \ \text{primo}(p), a∣pb ⟶ p∣a \lor a∣b
    \end{equation}
  \end{lema}

  Cuya formalización en Lean es la siguiente:
  \begin{leancode}
  lemma left_dvd_or_dvd_right_of_dvd_prime_mul
  [comm_cancel_monoid_with_zero α] {a : α} :
  ∀ {b p : α}, prime p → a ∣ p * b → p ∣ a ∨ a ∣ b
  \end{leancode}

  Entonces, como nosotros tenemos las hipótesis (\ref{h101q6teor}) y
  (\ref{h301q6teor}), podemos hacer uso del lema \ref{lemaprimo} y se tendría
  que ha de verificarse la siguiente disyunción:
  \begin{equation}\tag{h4}\label{h401q6teor}
    (ab+cd\ |\ ac+db) \lor (ac+bd\ |\ ad+bc).
  \end{equation}

  Con el objetivo de llegar a la contradicción que buscamos, vamos a separar
  la disyunción (\ref{h401q6teor}) y consideraremos los dos casos posibles.
  Se va a comprobar que en ambos casos llegamos a una contradicción.
  \begin{itemize}
  \item \textbf{Primer caso}

    Supongamos que se verifica la primera parte de la disyunción, es decir,
    que tenemos que
    \begin{equation}\tag{hj}\label{hj01q6teor}
      ab+cd\ |\ ac+db.
    \end{equation}

    En primer lugar, haciendo uso del lema \ref{lemades1}, probado en la
    sección anterior, se tiene que
    \begin{equation}\tag{hj1}\label{hj101q6}
      ab+cd > ac+bd.
    \end{equation}

    Además, haciendo uso de que la multiplicación de numeros positivos es
    positiva y la suma también, se tiene el siguiente resultado:
    \begin{equation}\label{hpj01q6}\tag{hpj}
      0 < ac+bd.
    \end{equation}

    A continuación, introduzcamos el siguiente teorema:
    \begin{teorema}\label{teoremaleq}
      Sean \(a\) y \(b\) dos números enteros tales que \(b\) es
      estrictamente mayor que cero y \(a\) divide a \(b\). Entonces, se
      tiene que \(a\) es un número menor o igual que
      \(b\).
    \end{teorema}

    Cuya formalización en Lean es la siguiente
    \begin{leancode}
    theorem le_of_dvd {a b : ℤ}
    (bpos : 0 < b)
    (H : a ∣ b) :
    a ≤ b
    \end{leancode}

    Entonces, se tiene que haciendo uso del teorema \ref{teoremaleq} sobre
    los números enteros \(ab+cd\), \(ac+bd\) y las hipótesis (\ref{hpj01q6})
    y (\ref{hj101q6}), se llega a que
    \begin{equation}\tag{hj2}\label{hj201q6}
      ab+cd≤ac+bd.
    \end{equation}

    Ahora bien, se sabe que la hipótesis (\ref{hj101q6}) es equivalente a
    esta otra:
    \begin{equation}\tag{hj3}\label{hj301q6}
      ¬(ab+cd≤ac+bd).
    \end{equation}

    De esta forma, si vemos las hipótesis (\ref{hj201q6}) y (\ref{hj301q6}),
    se puede observar que hemos llegado a una contradicción, que era lo
    deseado.

  \item \textbf{Segundo caso}

    Supongamos ahora que se verifica la segunda parte de la disyunción, es
    decir, que tenemos que
    \begin{equation}\tag{hk}\label{hk01q6teor}
      ac+bd\ |\ ad+bc
    \end{equation}

    Se va a proceder de manera totalmente análoga a como se ha hecho en el
    caso anterior.

    Comenzamos haciendo uso del lema auxliar \ref{lemades2}, a partir del
    cual se obtiene que
     \begin{equation}\tag{hk1}\label{hk101q6}
      ac+bd > ad+bc.
    \end{equation}

    Asimismo, como consecuencia de que el producto de números positivos
    es positivo y la suma de los mismos también es positiva, se puede
    afirmar que se verifica el siguiente resultado:
    \begin{equation}
      0<ad+bc.
    \end{equation}

    Por otro lado, se tiene que haciendo uso del teorema \ref{teoremaleq},
    visto anteriormente, se llega a que
    \begin{equation}\tag{hk2}\label{hk201q6}
      ac+bd≤ad+bc.
    \end{equation}

    Ahora bien, se tiene que la hipótesis (\ref{hk101q6}) puede ser
    reescrita de la siguiente manera:
    \begin{equation}\tag{hk3}\label{hk301q6}
      ¬(ac+bd≤ad+bc).
    \end{equation}

    Entonces, observando las hipótesis (\ref{hk201q6}) y (\ref{hk301q6}) se
    llega de nuevo a contradicción.
  \end{itemize}

  De esta forma, al haber llegado a contradicción en los dos casos que se
  diferenciaban en la disyunción (\ref{h401q6teor}), se tendría que la
  hipótesis que se llevó a cabo al principio de que el número \(ab+cd\) era
  primo es falsa, concluyendo así la prueba.
\end{demostracion}

La formalización en Lean de este teorema sería:
\begin{leancode}
theorem imo2001q6
  (hd  : 0 < d)
  (hdc : d < c)
  (hcb : c < b)
  (hba : b < a)
  (h : a*c + b*d = (a + b - c + d)*(-a + b + c + d))
  : ¬ prime (a*b + c*d) :=
begin
  intro h1,
  have ha : 0 < a,
    by linarith,
  have hb : 0 < b,
    by linarith,
  have hc : 0 < c,
    by linarith,
  have h2 : (a*c + b*d) * (b^2 + b*d + d^2) =
            (a*b + c*d) * (a*d + b*c),
    from productos_equivalentes h,
  have h3 : a*c + b*d ∣ (a*b + c*d) * (a*d + b*c),
    from division h,
  have h4: (a*b + c*d ∣ a*c + b*d) ∨ (a*c + b*d  ∣ a*d + b*c),
    from left_dvd_or_dvd_right_of_dvd_prime_mul h1 h3,
  cases h4 with hj hk,
  { have hj1: a*b + c*d > a*c + b*d,
      from desigualdad_auxiliar1 hba hcb hdc h,
    have hpj: 0 < a*c + b*d,
      from add_pos (mul_pos ha hc) (mul_pos hb hd),
    have hj2: a*b + c*d ≤ a*c + b*d,
      from int.le_of_dvd hpj hj,
    have hj3: ¬ (a*b + c*d ≤ a*c + b*d),
      from not_le.mpr hj1,
    exact hj3 hj2, },
  { have hk1: a*c + b*d > a*d + b*c,
      from desigualdad_auxiliar2 hba hdc h,
    have hpk: 0 < a*d + b*c,
      from add_pos (mul_pos ha hd) (mul_pos hb hc),
    have hk2: a*c + b*d ≤ a*d + b*c,
      from int.le_of_dvd hpk hk,
    have hk3: ¬ (a*c+b*d ≤  a*d+b*c),
      from not_le.mpr hk1,
    exact hk3 hk2, },
end
\end{leancode}
\index{imo2001q6}

Para la formalización de este teorema en Lean han sido necesarios los
siguiente lemas y teoremas auxiliares:
\begin{itemize}
\item \mint{lean}|left_dvd_or_dvd_right_of_dvd_prime_mul: prime p → a ∣ p*b → p∣a ∨ a∣b|
  \indLema{left\_dvd\_or\_dvd\_right\_of\_dvd\_prime\_mul}
\item \mint{lean}|add_pos : 0 < a → 0 < b → 0 < a + b|
  \indLema{add\_pos}
\item \mint{lean}|mul_pos : 0 < a → 0 < b → 0 < a * b|
  \indLema{mul\_pos}
\item \mint{lean}|le_of_dvd : 0 < b → a ∣ b → a ≤ b|
  \indLema{le\_of\_dvd}
\item \mint{lean}| not_le : ¬ a ≤ b ↔ b < a |
  \indLema{not\_le}
\end{itemize}

Además, se han usado las tácticas
\tactica{exact}{exact},
\tactica{have}{have},
\tactica{intro}{intro},
\tactica{cases}{cases} y
\tactica{linarith}{linarith}.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "IMO_en_Lean"
%%% End:
